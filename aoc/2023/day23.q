d23p1:{lens:();
    step:0;
    queue:enlist`pos`d!(0,first where"."=x 0;2);
    while[count queue;
        if[count finished:select from queue where pos[;0]=count[x]-1;
            lens,:count[finished]#step;
            queue:delete from queue where pos[;0]=count[x]-1;
        ];
        nxts:update t:x ./:pos from queue;
        nxts:update j:i, nd:enlist each(".^>v<"!0N 0 1 2 3)t from nxts;
        nxts:update nd:(d+count[i]#enlist -1 0 1)mod 4 from nxts where t=".";
        nxts:delete j from(ungroup delete pos from nxts)lj 1!select j,pos from nxts;
        nxts:update pos+(-1 0;0 1;1 0;0 -1)nd from nxts;
        nxts:update t:x ./:pos from nxts;
        nxts:delete from nxts where t in " #";
        nxts:delete from nxts where t="v<^>"nd;
        queue:select pos,d:nd from nxts;
        step+:1;
    ];
    last lens};
d23p2:{x:ssr[;"[<>v^]";"."]each x;
    poi:raze til[count x],/:'where each 2<(x=".")*sum"."=
        (("#",/:-1_/:x);(1_/:x,\:"#");(1#x),-1_x;(1_x),-1#x);
    poi:(enlist 0,first where x[0]="."),poi,enlist (count[x]-1),first where last[x]=".";
    conn:til[count poi]!count[poi]#enlist();
    step:0;
    queue:([]start:til count poi;pos:poi)cross([]d:til 4);
    while[count queue;
        if[step>0;
            finish:select from queue where pos in poi;
            if[count finish;
                newc:exec((poi?pos),'step) by start from finish;
                conn[key newc]:conn[key newc],'value newc;
                queue:delete from queue where pos in poi;
            ];
        ];
        nxts:update j:i, nd:(d+count[i]#enlist -1 0 1)mod 4 from queue;
        nxts:delete j from(ungroup delete pos from nxts)lj 1!select j,pos from nxts;
        nxts:update pos+(-1 0;0 1;1 0;0 -1)nd from nxts;
        nxts:update t:x ./:pos from nxts;
        nxts:delete from nxts where t in " #";
        queue:distinct select start, pos,d:nd from nxts;
        step+:1;
    ];
    queue:enlist`node`len`p!(0;0;enlist 0);
    maxlen:0;
    while[count queue;
        finish:select from queue where node=count[poi]-1;
        if[count finish;
            maxlen|:exec max len from finish;
            queue:delete from queue where node=count[poi]-1;
        ];
        nxts:update j:i, nn:conn node from queue;
        nxts:(ungroup delete p from nxts) lj 1!select j,p from nxts;
        nxts:delete from nxts where nn[;0] in'p;
        nxts:update node:nn[;0], len+nn[;1], p:(p,'nn[;0]) from nxts;
        queue:select node,len,p from nxts;
        nxts:();
        .Q.gc[];
        show count queue;
    ];
    maxlen};

/
x:();
x,:enlist"#.#####################";
x,:enlist"#.......#########...###";
x,:enlist"#######.#########.#.###";
x,:enlist"###.....#.>.>.###.#.###";
x,:enlist"###v#####.#v#.###.#.###";
x,:enlist"###.>...#.#.#.....#...#";
x,:enlist"###v###.#.#.#########.#";
x,:enlist"###...#.#.#.......#...#";
x,:enlist"#####.#.#.#######.#.###";
x,:enlist"#.....#.#.#.......#...#";
x,:enlist"#.#####.#.#.#########v#";
x,:enlist"#.#...#...#...###...>.#";
x,:enlist"#.#.#v#######v###.###v#";
x,:enlist"#...#.>.#...>.>.#.###.#";
x,:enlist"#####v#.#.###v#.#.###.#";
x,:enlist"#.....#...#...#.#.#...#";
x,:enlist"#.#########.###.#.#.###";
x,:enlist"#...###...#...#...#.###";
x,:enlist"###.###.#.###v#####v###";
x,:enlist"#...#...#.#.>.>.#.>.###";
x,:enlist"#.###.###.#.###.#.#v###";
x,:enlist"#.....###...###...#...#";
x,:enlist"#####################.#";

d23p1 x //94
d23p2 x //154
